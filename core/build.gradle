plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

dependencies {
    // ideally matches the dependency version of the latest NMS module
    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly('LibsDisguises:LibsDisguises:10.0.28') {
        exclude group: 'org.spigotmc'
        exclude group: 'com.comphenix.protocol'
    }
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'org.black_ixx:playerpoints:3.1.0'
    // WG 7.0.3 was last version to support Java <16
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.3'
    // same version used in 1.17.1, 3.x uses Java 16
    compileOnly 'com.mojang:authlib:2.3.31'
    // used by the update checker
    compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
    // NMS modules can't actually use XSeries but it doesn't compile without 'api'.
    // IDEs do not like it when 'core' references XSeries directly
    // but subprojects must reference XSeries as shaded.
    api 'com.github.cryptomorin:XSeries:8.8.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    compileOnly 'net.luckperms:api:5.4'
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'net.wesjd:anvilgui:1.5.3-SNAPSHOT'
    implementation 'com.github.GamerCoder215.MobChip:mobchip-bukkit:1.5.0'
}
shadowJar {
    // removes unused classes in 'implementation' path dependencies
    minimize {
        // Loads classes reflectively
        exclude(dependency('net.wesjd:anvilgui:.*'))
        exclude(dependency('com.github.GamerCoder215.MobChip:.*:.*'))
    }
    def path = 'be.isach.ultracosmetics.shaded'
    // XParticle is big and not required so don't shade it.
    // It's not removed by minimize() because it's on the API path I guess?
    exclude 'com/cryptomorin/xseries/particles/*'
    relocate ('com.zaxxer.hikari', path + '.hikari')
    relocate ('com.cryptomorin.xseries', path + '.xseries')
    relocate ('org.bstats', path + '.metrics')
    relocate ('net.wesjd', path + '.anvilgui')
    relocate ('me.gamercoder215.mobchip', path + '.mobchip')
}
